package com.javacodegeeks.drools;

rule "Supertype Inheritance Inference"
when

    $instance: InstanceOfClazz( clazz.parent !=null   ) 
    not ( exists(  InstanceOfClazz( name==$instance.name , clazz == clazz.parent ) ) )
then
    System.out.println( "Supertype Inheritance --> New InstanceOfClass has to be created with name: "+ $instance.getName() +" and class: "+$instance.getClazz().getParent().getName() );
    InstanceOfClazz newinstanceofclazz = new InstanceOfClazz($instance.getName(),$instance.getClazz().getParent());
    insert(newinstanceofclazz);
end


rule "Class Transitiveness Inference"
when
    $clazz: Clazz( parent != null , parent.parent != null  ) 
    not ( exists(  Clazz( name==$clazz.name , parent == $clazz.parent.parent ) ) )
then
    System.out.println( "Class Transitiveness --> New Class has to be created with name: "+ $clazz.getName() +" and parent: "+$clazz.getParent().getParent().getName() );
    Clazz newclazz = new Clazz($clazz.getName(),$clazz.getParent().getParent());
    insert(newclazz);
end


rule "Consistency Checking of Triples based on Domain Restrictions"
when
    $triple: KnowledgeTriple() 
    not ( exists(  InstanceOfClazz( name == $triple.subject.name , clazz == $triple.predicate.domain ) ) )
then
    System.out.println( "Modelling Error! Invalid Triple: " + $triple );
    retract($triple);
end

rule "Consistency Checking of Triples based on Range Restrictions"
when
    $triple: KnowledgeTriple() 
    not ( exists(  InstanceOfClazz( name == $triple.object.name  , clazz == $triple.predicate.range  ) ) )
then
    System.out.println( "Modelling Error! Invalid Triple: " + $triple );
    retract($triple);
end

rule "Knowledge Expansion through Range Generalization (make use of Supertype inheritance)"
when
    $triple: KnowledgeTriple() 
    $objectinstance: InstanceOfClazz(name == $triple.object.name, clazz != $triple.object.clazz )
    not ( 
        exists(      
            $newtriple: KnowledgeTriple( predicate == $triple.predicate , subject == $triple.subject, object.name == $triple.object.name , object.clazz != $triple.object.clazz  ) 
        ) 
    )
then
    System.out.println( "Knowledge Expansion through Range Generalization  --> New Triple has to be created Predicate:"+$triple.getPredicate().getName()+" Subject: "+$triple.getSubject().getName() +"( "+$triple.getSubject().getClazz().getName()+" )"+" Object: "+$objectinstance.getName() + " ( "+$objectinstance.getClazz().getName()+" )" );
    KnowledgeTriple newtriple = new KnowledgeTriple( $triple.getSubject() , $triple.getPredicate() , $objectinstance );
    insert(newtriple);
end


rule "Knowledge Expansion through Domain Generalization (make use of Supertype inheritance)"
when
    $triple: KnowledgeTriple() 
    $subjectinstance: InstanceOfClazz(name == $triple.subject.name, clazz != $triple.subject.clazz )
    not ( 
        exists(      
            $newtriple: KnowledgeTriple( predicate == $triple.predicate , object == $triple.object, subject.name == $triple.subject.name , subject.clazz != $triple.subject.clazz  ) 
        ) 
    )
then
    System.out.println( "Knowledge Expansion through Domain Generalization --> New Triple has to be created Predicate:"+$triple.getPredicate().getName()+" Subject: "+$subjectinstance.getName() +"("+$subjectinstance.getClazz().getName()+")"+" Object: "+$triple.getObject().getName() +"("+$triple.getObject().getClazz().getName()+")" );
    KnowledgeTriple newtriple = new KnowledgeTriple( $subjectinstance , $triple.getPredicate() , $triple.getObject() );
    insert(newtriple);
end


rule "Knowledge Expansion through Transitive Property Interpretation"
when
    $pred: ObjectProperty(transitive == true )
    $triple1: KnowledgeTriple( predicate == $pred , $subject1: subject , $object1: object ) 
    $triple2: KnowledgeTriple( predicate == $pred , subject == $object1) 

    not ( 
        exists(      
            KnowledgeTriple( predicate == $pred , subject == $triple1.subject, object == $triple2.object  ) 
        ) 
    )
then
    System.out.println( "Knowledge Expansion through Transitive Property   --> New Triple has to be created Predicate:"+$pred.getName()+" Subject: "+$triple1.getSubject().getName()+"("+$triple1.getSubject().getClazz().getName()+") Object: "+$triple2.getObject().getName()+"("+$triple2.getObject().getClazz().getName()+")" );
    KnowledgeTriple newtriple = new KnowledgeTriple( $triple1.getSubject() , $pred , $triple2.getObject() );
    insert(newtriple);
end


/*
rule "Debug-PrintAllClasses"
when
    $clazz: Clazz() 
then
    System.out.println( "Clazz --> "+$clazz  );
end

rule "Debug-PrintAllInstances"
when
    $instanceofclass: InstanceOfClazz() 
then
    System.out.println( "InstanceOfClazz --> "+$instanceofclass  );
end

rule "Debug-PrintAllObjectProperties"
when
    $objectproperty: ObjectProperty() 
then
    System.out.println( "ObjectProperty --> "+$objectproperty  );
end

rule "Debug-PrintAllTriples"
when
    $triple: KnowledgeTriple() 
then
    System.out.println( "KnowledgeTriple --> "+$triple  );
end
*/